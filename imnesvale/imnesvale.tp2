BACKUP ~weidu_external/imnesvale/backup~
//AUTHOR ~Duality (Dualities.Shadow@gmail.com)~
SUPPORT ~https://github.com/Spellhold-Studios/Duality-Odd-Quest-Mod~

VERSION ~v2.2.2~

README ~imnesvale/lang/%LANGUAGE%/imnesvale-readme.txt~ ~imnesvale/imnesvale-readme.txt~


ALWAYS

	ACTION_IF (!VARIABLE_IS_SET bg2_chapter) BEGIN // Run only once per install

		// Determines which game is installed and if the game is an Enhanced Edition
		OUTER_SET is_eet = (GAME_IS "eet") ? 1 : 0						// eet

		/* eet compatibility */
		ACTION_IF (is_eet) BEGIN
			OUTER_SET bg2_chapter = 12
		END ELSE BEGIN
			OUTER_SET bg2_chapter = 0
		END
		OUTER_FOR (i = 1 ; i <= 10 ; ++i) BEGIN
			OUTER_SET bg2_chapter = bg2_chapter + 1
			OUTER_SPRINT name_source ~bg2_chapter_%i%~
			OUTER_SET EVAL ~%name_source%~ = bg2_chapter
		END


		// Convert TRA character encoding for the original game edition
		ACTION_DEFINE_ARRAY du#noconvert BEGIN setup END

		LAF HANDLE_CHARSETS
			INT_VAR
				infer_charsets = 1
				from_utf8 = 1
			STR_VAR
				default_language = ~english~
				tra_path = EVAL ~%MOD_FOLDER%/lang~
				out_path = EVAL ~weidu_external/%MOD_FOLDER%/lang~
				noconvert_array = ~du#noconvert~
		END

		ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
			OUTER_TEXT_SPRINT tra_location EVAL ~%MOD_FOLDER%/lang~
		END ELSE BEGIN
			OUTER_TEXT_SPRINT tra_location EVAL ~weidu_external/%MOD_FOLDER%/lang~
		END

		LOAD_TRA ~%tra_location%/english/misc.tra~
				 ~%tra_location%/%LANGUAGE%/misc.tra~

	END

END


AUTO_TRA ~%tra_location%/%s~

LANGUAGE ~English~
		 ~english~
		 ~imnesvale/lang/english/setup.tra~

LANGUAGE ~Espanol (traduccion de alaisFcZ)~
		 ~spanish~
		 ~imnesvale/lang/english/setup.tra~
		 ~imnesvale/lang/spanish/setup.tra~

LANGUAGE ~Deutsch (Uebersetzung von BiG World Textpack German project)~
		 ~german~
		 ~imnesvale/lang/english/setup.tra~
		 ~imnesvale/lang/german/setup.tra~

LANGUAGE ~Francais (traduction par Deratiseur)~
		 ~french~
		 ~imnesvale/lang/english/setup.tra~
		 ~imnesvale/lang/french/setup.tra~

LANGUAGE ~Italiano (traduzione di ilot)~
		 ~italian~
		 ~imnesvale/lang/english/setup.tra~
		 ~imnesvale/lang/italian/setup.tra~

LANGUAGE ~Russian (translation by prowler)~
		 ~russian~
		 ~imnesvale/lang/english/setup.tra~
		 ~imnesvale/lang/russian/setup.tra~


/* ====================== *
 *  Imnesvale Mod : Easy  *
 * ====================== */
BEGIN @1											// ~Easy~
SUBCOMPONENT @0 LABEL ~imnesvale_easy~				// ~Install Imnesvale Mod~
REQUIRE_PREDICATE GAME_IS ~tob bgt bg2ee eet~ @4	// ~You must have ToB, BGT, BG2:EE or EET installed to install this mod.~

OUTER_SPRINT difficulty "easy"
INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~


/* ====================== *
 *  Imnesvale Mod : Hard  *
 * ====================== */
BEGIN @2			// ~Hard~
SUBCOMPONENT @0 LABEL ~imnesvale_hard~
REQUIRE_PREDICATE GAME_IS ~tob bgt bg2ee eet~ @4

OUTER_SPRINT difficulty "hard"
INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~


/* ===================================================== *
 *  Imnesvale Mod : Are You Out Of Your Freaking Mind?!  *
 * ===================================================== */
BEGIN @3			// ~Are You Out Of Your Freaking Mind?!~
SUBCOMPONENT @0 LABEL ~imnesvale_impossible~
REQUIRE_PREDICATE GAME_IS ~tob bgt bg2ee eet~ @4

OUTER_SPRINT difficulty "impossible"
INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~
